allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'eclipse-wtp'
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  group = 'org.ardverk.gibson'
  version = '0.3'

  repositories {
    mavenCentral()
    mavenLocal()
    
    maven {
      url "http://morphia.googlecode.com/svn/mavenrepo"
    }
  }

  configurations { 
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    deployerJars
  }
  
  dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
  }
  
  if (System.properties['ardverk.key']) {
    uploadArchives {
      repositories.mavenDeployer {
        name = 'sshDeployer' // optional
        configuration = configurations.deployerJars
        repository(url: "scp://mvn.ardverk.org/repository/release") {
          authentication(
            userName: System.properties['ardverk.username'],
            privateKey: new File(System.properties['ardverk.key']).absolutePath,
            passphrase: System.properties['ardverk.passphrase']
          )
        }
      }
    }
  }
  
  project.ext {
    slf4j = "1.6.6"
    commonsio = "2.4"
    commonscodec = "1.7"
    commonslang = "3.1"
    junit = "4.10"
  }
}

dependencies {
  compile project(":gibson-core")
  compile project(":gibson-appender")
}
