allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'eclipse-wtp'
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  sourceCompatibility = 1.6

  group = 'org.ardverk.gibson'
  version = '0.1'

  repositories {
    mavenCentral()
    mavenLocal()
    
    maven {
      url "http://morphia.googlecode.com/svn/mavenrepo"
    }
  }

  configurations { 
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    deployerJars
  }
  
  dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
  }
  
  uploadArchives {
    repositories.mavenDeployer {
      name = 'sshDeployer' // optional
      configuration = configurations.deployerJars
      repository(url: "scp://mvn.ardverk.org/repository/release") {
        authentication(
          userName: System.properties['ardverk.username'],
          privateKey: new File(System.properties['ardverk.key']).absolutePath,
          passphrase: System.properties['ardverk.passphrase']
        )
      }
    }
  }
  
  project.ext {
    slf4j = "1.6.4"
    commonsio = "2.3"
    commonscodec = "1.6"
    commonslang = "2.6"
    junit = "4.10"
  }
}

dependencies {
  compile project(":gibson-core")
  compile project(":gibson-appender")
}
