buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/groups/public' }
  }
  dependencies {
    classpath 'com.github.townsfolk:gradle-release:1.2'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.11'
}

apply plugin: 'release'

allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
  
  group = 'com.squarespace.gibson'
  
  repositories {
    mavenCentral()
    mavenLocal()
  }
  
  project.ext {
    releaseRepositoryUrl = System.getenv('RELEASE_REPOSITORY_URL')
    snapshotRepositoryUrl = System.getenv('SNAPSHOT_REPOSITORY_URL')
  }
  
  dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.1'
    compile 'commons-codec:commons-codec:1.9'
  }
  
  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }
  
  artifacts {
    archives sourcesJar
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: project.releaseRepositoryUrl)
      snapshotRepository(url: project.snapshotRepositoryUrl)
    }
  }
}


createReleaseTag.dependsOn ':uploadArchives'